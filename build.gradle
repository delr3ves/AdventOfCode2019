plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
}
apply plugin: 'kotlin-kapt'

def arrow_version = "0.10.3"


group 'dev.serch'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
}

configurations {
    ktlint
}

test {
    useJUnitPlatform()
}

dependencies {
    ktlint "com.pinterest:ktlint:0.33.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.arrow-kt:arrow-core:$arrow_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    kapt "io.arrow-kt:arrow-meta:$arrow_version"
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn(ktlint)

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
